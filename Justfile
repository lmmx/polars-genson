default: clippy

# lint:    ty ruff-check
lint: ruff-check
lint-ci: clippy-ci
# lint-ci: ty-ci ruff-check

fmt:     ruff-fmt code-quality-fix

precommit:     lint fmt code-quality
precommit-ci:  lint-ci  code-quality
precommit-fix: fmt      code-quality-fix

prepush: clippy py-test py-dev

ci: precommit prepush docs

# Full development workflow
full: check clippy-all build test py-dev py-test

# CI workflow
ci-full: precommit-ci prepush py-dev py-test docs

e:
    $EDITOR Justfile

# -------------------------------------

build:
    cargo build --workspace

# Check all projects
check:
    cargo check --workspace

# Fast individual package checks
check-core:
    cargo check -p genson-core

check-cli:
    cargo check -p genson-cli

check-py:
    cargo check -p polars-genson-py

# -------------------------------------

clippy: clippy-all

clippy-all:
    cargo clippy --workspace --all-targets --all-features --target-dir target/clippy-all-features -- -D warnings

# Fast clippy for individual packages
clippy-core:
    cargo clippy -p genson-core -- -D warnings

clippy-cli:
    cargo clippy -p genson-cli -- -D warnings

clippy-py:
    cargo clippy -p polars-genson-py -- -D warnings

vendor-ci:
    mkdir -p .vendored
    cargo vendor-filterer --versioned-dirs --platform=x86_64-unknown-linux-gnu .vendored/vendored.tar.gz --format=tar.gz

clippy-ci:
    #!/usr/bin/env echo-comment
    set -e
    # Start: $(date)
    # ðŸ” CI Environment Debug Information
    # Current directory: $(pwd)
    # Rust available: $(which rustc || echo 'none')
    # Cargo available: $(which cargo || echo 'none')

    ## Check if vendor directory exists, if not extract from compressed vendored dependencies
    if [ ! -d ".vendored" ] || [ ! "$(ls -A .vendored 2>/dev/null | grep -v vendored.tar.gz)" ]; then
        # ðŸ“¦ Extracting compressed vendored dependencies for CI...
        if [ -f ".vendored/vendored.tar.gz" ]; then
            # Found compressed vendored dependencies, extracting...
            cd .vendored
            tar -xzf vendored.tar.gz
            cd ..
            
            ## Fix .cargo/config.toml with current absolute path
            if [ -f ".cargo/config.toml" ]; then
                CURRENT_DIR=$(pwd)
                sed -i "s|PLACEHOLDER_DIR|${CURRENT_DIR}/.vendored|g" ".cargo/config.toml"
                # âœ“ .cargo/config.toml updated with current directory: $CURRENT_DIR
                # Updated .cargo/config.toml contents:
                cat ".cargo/config.toml"
            else
                # No .cargo/config.toml found, creating temporary one
                mkdir -p .cargo
                cat > .cargo/config.toml << 'EOF'
    [source.crates-io]
    replace-with = "vendored-sources"

    [source.vendored-sources]
    directory = ".vendored"
    EOF
                # Mark for cleanup
                CREATED_CARGO_CONFIG=true
            fi

            # âœ… Extraction complete, running diagnostics...

            ## Diagnostic checks
            # ðŸ” Vendor structure check:
            ls -la .vendored/ | head -5
            #

            # ðŸ” Cargo config check:
            if [ -f ".cargo/config.toml" ]; then
                # Cargo config exists
                grep -E "(source|directory)" .cargo/config.toml || echo "âŒ Cargo config check failed"
            else
                # âŒ No .cargo/config.toml found
                ls -la .cargo/ | head -5 2>/dev/null || echo "âŒ No .cargo directory found"
            fi

            # ðŸ” Vendored crates check:
            VENDOR_CRATES=".vendored"
            if ls $VENDOR_CRATES >/dev/null 2>&1; then
                # Vendor directory exists:
                find $VENDOR_CRATES -maxdepth 1 -type d -name "*-*" | wc -l | xargs -I {} echo "Found {} vendored crate directories"
            else
                # âŒ No vendor directory found
            fi

            # ðŸ” Offline build test:
            export CARGO_NET_OFFLINE=true
            export CARGO_HOME="$(pwd)/.cargo"

            # Active Cargo: $(which cargo)
            cargo --version || echo "âŒ Cargo activation failed"

            # ðŸ” Critical dependency test:
            cargo metadata --format-version 1 --offline --no-deps >/dev/null 2>&1 && echo "âœ“ Cargo metadata successful" || echo "âŒ Cargo metadata failed"
            cargo check --offline --quiet >/dev/null 2>&1 && echo "âœ“ Offline dependency resolution successful" || echo "âŒ Offline dependency resolution failed"

        else
            # âŒ No vendored dependencies found, cannot proceed offline...
            # ERROR: .vendored/vendored.tar.gz not found - run 'just vendor-ci' first
            exit 1
        fi
    else
        # âœ… .vendored directory already exists with crates, activating...
        export CARGO_NET_OFFLINE=true
        export CARGO_HOME="$(pwd)/.cargo"
    fi

    # ðŸš€ Running clippy check...
    cargo clippy --offline --workspace --target-dir target/clippy --no-deps -- -D warnings

    # ðŸ§¹ Cleanup: restore original state
    # Clean up extracted vendored dependencies (keep only the tarball)
    if [ -d ".vendored" ] && [ -f ".vendored/vendored.tar.gz" ]; then
        # Remove all extracted crate directories but keep the tarball
        find .vendored -maxdepth 1 -type d -name "*-*" -exec rm -rf {} +
        # âœ“ Cleaned up extracted vendored dependencies
    fi
    
    if [ "$CREATED_CARGO_CONFIG" = "true" ]; then
        # Removing temporary .cargo directory that we created (including any cache files)
        rm -rf .cargo
        # âœ“ Restored original state (no .cargo config)
    fi
    # End: $(date)

# -------------------------------------

test *args:
    just test-core {{args}}
    just test-cli {{args}}
    just test-js {{args}}

[working-directory: 'genson-core']
test-core *args:
    cargo nextest run {{args}}
    
[working-directory: 'genson-cli']
test-cli *args:
    cargo nextest run {{args}}
    
test-pl *args:
    just test-py {{args}}

[working-directory: 'polars-jsonschema-bridge']
test-js *args:
    cargo nextest run {{args}}


test-ci *args:
    #!/usr/bin/env -S echo-comment --color bright-green
    # ðŸƒ Running Rust tests...
    cargo test {{args}}
    
    # ðŸ“š Running documentation tests...
    cargo test --doc {{args}}

# -------------------------------------

[working-directory: 'polars-genson-py']
ruff-check mode="":
   ruff check . {{mode}}

[working-directory: 'polars-genson-py']
ruff-fix:
   just ruff-check --fix

[working-directory: 'polars-genson-py']
ruff-fmt:
   ruff format .

# Type checking
[working-directory: 'polars-genson-py']
ty *args:
   #!/usr/bin/env bash
   ty check . --exit-zero {{args}} 2> >(grep -v "WARN ty is pre-release software" >&2)

t:
   just ty --output-format=concise

[working-directory: 'polars-genson-py']
ty-ci:
    #!/usr/bin/env -S echo-comment --shell-flags="-e" --color blue
    # ðŸ” CI Environment Debug Information
    # Current directory: $(pwd)
    # Python available: $(which python3 || echo 'none')
    # UV available: $(which uv || echo 'none')
    
    ## Check if .venv exists, if not extract from compressed CI venv
    if [ ! -d ".venv" ]; then
        # ðŸ“¦ Extracting compressed stubs for CI...
        if [ -f ".stubs/venv.tar.gz" ]; then
            # Found compressed stubs, extracting...
            tar -xzf .stubs/venv.tar.gz
            mv venv .venv
            
            ## Fix pyvenv.cfg with current absolute path
            if [ -f ".venv/pyvenv.cfg" ]; then
                CURRENT_DIR=$(pwd)
                sed -i "s|PLACEHOLDER_DIR|${CURRENT_DIR}/.venv|g" ".venv/pyvenv.cfg"
                # âœ“ pyvenv.cfg updated with current directory: $CURRENT_DIR
                # Updated pyvenv.cfg contents:
                cat ".venv/pyvenv.cfg"
            fi
            
            # âœ… Extraction complete, running diagnostics...
            
            ## Diagnostic checks
            # ðŸ” Venv structure check:
            ls -la .venv/ | head -5
            #
            
            # ðŸ” Python interpreter check:
            if [ -f ".venv/bin/python" ]; then
                # Python executable exists
                .venv/bin/python --version || echo "âŒ Python version check failed"
            else
                # âŒ No Python executable found
                ls -la .venv/bin/ | head -5
            fi
            
            # ðŸ” Site-packages check:
            SITE_PACKAGES=".venv/lib/python*/site-packages"
            if ls $SITE_PACKAGES >/dev/null 2>&1; then
                # Site-packages directory exists:
                ls $SITE_PACKAGES | grep -E "(polars|polars_genson)" || echo "âŒ Key packages not found"
            else
                # âŒ No site-packages directory found
            fi
            
            # ðŸ” Environment activation test:
            export PATH="$(pwd)/.venv/bin:$PATH"
            export VIRTUAL_ENV="$(pwd)/.venv"
            export PYTHONPATH=""  # Clear any existing PYTHONPATH
            
            # Active Python: $(which python)
            python --version || echo "âŒ Python activation failed"
            
            # ðŸ” Critical imports test:
            python -c 'import sys; print("âœ“ Python sys module working"); print("Python executable:", sys.executable)' || echo "âŒ Basic Python test failed"
            python -c 'import polars as pl; print("âœ“ Polars import successful, version:", pl.__version__)' || echo "âŒ Polars import failed"
            python -c 'import polars_genson; print("âœ“ Polars Genson import successful")' || echo "âŒ Polars Genson import failed"
            python -c 'import pytest; print("âœ“ Pytest import successful")' || echo "âŒ Pytest import failed"
            
        else
            # âŒ No stubs found, running regular setup...
            just setup
        fi
    else
        # âœ… .venv already exists, activating...
        export PATH="$(pwd)/.venv/bin:$PATH"
        export VIRTUAL_ENV="$(pwd)/.venv"
    fi
    
    # ðŸš€ Running ty check...
    just t

# -------------------------------------

[working-directory: 'polars-genson-py']
pf:
    pyrefly check . --output-format=min-text

# -------------------------------------

# Test CLI with example input
run-cli input="'{\"name\": \"test\", \"value\": 42}'":
    echo '{{input}}' | cargo run -p genson-cli

# Run CLI with file
run-cli-on *args:
    cargo run -p genson-cli -- {{args}}

# -------------------------------------

# Develop Python plugin (debug mode)
[working-directory: 'polars-genson-py']
py-dev:
    $(uv python find) -m maturin develop

# Develop Python plugin (release mode)  
[working-directory: 'polars-genson-py']
py-release:
    $(uv python find) -m maturin develop --release

# Test Python plugin with pytest
[working-directory: 'polars-genson-py']
py-test:
    #!/usr/bin/env bash
    $(uv python find) -m pytest tests/

# Quick test to verify basic functionality  
[working-directory: 'polars-genson-py']
py-quick:
    #!/usr/bin/env bash
    python -c "
    import polars as pl
    import polars_genson
    import json
    
    print('Testing polars-genson plugin...')
    
    df = pl.DataFrame({
        'json_data': [
            '{\"name\": \"Alice\", \"age\": 30}',
            '{\"name\": \"Bob\", \"age\": 25, \"city\": \"NYC\"}',
            '{\"name\": \"Charlie\", \"age\": 35, \"email\": \"charlie@example.com\"}'
        ]
    })
    
    print('Input DataFrame:')
    print(df)
    
    schema = df.genson.infer_json_schema('json_data')
    print('\nInferred schema:')
    print(json.dumps(schema, indent=2))
    
    # Verify schema structure
    assert 'type' in schema
    assert 'properties' in schema
    props = schema['properties']
    assert 'name' in props
    assert 'age' in props
    
    print('\nâœ… Schema inference successful!')
    print(f'Found properties: {list(props.keys())}')
    "

# -------------------------------------

install-hooks:
   pre-commit install

run-pc:
   pre-commit run --all-files

[working-directory: 'polars-genson-py']
setup:
   #!/usr/bin/env bash
   uv venv
   source .venv/bin/activate
   uv sync

[working-directory: 'polars-genson-py']
sync:
   uv sync

# -------------------------------------

fix-eof-ws mode="":
    #!/usr/bin/env sh
    ARGS=''
    if [ "{{mode}}" = "check" ]; then
        ARGS="--check-only"
    fi
    whitespace-format --add-new-line-marker-at-end-of-file \
          --new-line-marker=linux \
          --normalize-new-line-markers \
          --exclude ".git/|target/|dist/|\.so$|.json$|.lock$|.parquet$|.venv/|.stubs/|\..*cache/" \
          $ARGS \
          .

code-quality:
    # just ty-ci
    taplo lint
    taplo format --check
    just fix-eof-ws check
    cargo machete
    cargo fmt --check --all

code-quality-fix:
    taplo lint
    taplo format
    just fix-eof-ws
    cargo machete
    cargo fmt --all

# -------------------------------------

docs:
    cargo doc --workspace --all-features --no-deps --document-private-items --keep-going

# -------------------------------------

clean:
    cargo clean
    rm -rf polars-genson-py/target

# -------------------------------------

# Example: JSON schema inference
example-basic:
    just test-cli '{"name": "Alice", "age": 30}'

example-array:
    just test-cli '[{"name": "Alice", "age": 30}, {"name": "Bob", "age": 25, "city": "NYC"}]'

example-complex:
    echo '{"users": [{"name": "Alice", "profile": {"age": 30, "active": true}}, {"name": "Bob", "profile": {"age": 25, "premium": false}}]}' | just run-cli

# -------------------------------------

[working-directory: 'polars-genson-py']
refresh-stubs *args="":
    #!/usr/bin/env -S echo-comment --shell-flags="-e" --color bright-green
    rm -rf .stubs
    set -e  # Exit on any error
    
    ## Check if --debug flag is passed and export DEBUG_PYSNOOPER
    debug_flag=false
    uv_args="--no-group debug"
    # Args received: {{args}}
    if [[ "{{args}}" == *"--debug"* ]]; then
        export DEBUG_PYSNOOPER=true
        # DEBUG MODE: ON
        debug_flag=true
        uv_args=""  # Remove --no-group debug when in debug mode
    fi
    
    uv sync --no-group build $uv_args
    ./stub_gen.py
    deactivate
    mv .venv/ offvenv
    just run-pc
    rm -rf .venv
    mv offvenv .venv
    
    ## Unset DEBUG_PYSNOOPER if it was set
    if [[ "$debug_flag" == "true" ]]; then
        unset DEBUG_PYSNOOPER
    fi


# Release a new version, pass --help for options to `uv version --bump`
[working-directory: 'polars-genson-py']
release bump_level="patch":
    #!/usr/bin/env -S echo-comment --shell-flags="-e" --color blue

    ## Exit early if help was requested
    if [[ "{{bump_level}}" == "--help" ]]; then
        uv version --help
        exit 0
    fi

    uv version --bump {{bump_level}}
    
    git add --all
    git commit -m "chore(temp): version check"
    new_version=$(uv version --short)
    git reset --soft HEAD~1
    git add --all
    git commit -m  "chore(release): bump -> v$new_version"
    branch_name=$(git rev-parse --abbrev-ref HEAD);
    git push origin $branch_name
    uv build
    uv publish -u __token__ -p $(keyring get PYPIRC_TOKEN "")
